/*
 * This source is part of the
 *      _____  ___   ____
 *  __ / / _ \/ _ | / __/___  _______ _
 * / // / , _/ __ |/ _/_/ _ \/ __/ _ `/
 * \___/_/|_/_/ |_/_/ (_)___/_/  \_, /
 *                              /___/
 * repository.
 *
 * Copyright (C) 2015 Carmen Alvarez (c@rmen.ca)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "ca.rmen.android.networkmonitor"
        minSdkVersion 9
        targetSdkVersion 25
        versionName "1.28.10"
        versionCode 12810
        archivesBaseName += "-" + versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }

    productFlavors {
        foss {

        }
        proprietary {

        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {

    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:preference-v7:25.0.1'
    compile 'com.android.support:preference-v14:25.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile ('net.sourceforge.jexcelapi:jxl:2.6.12') {
        exclude group: "log4j"
    }
    compile 'commons-net:commons-net:3.5'
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.1'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.2.1'

    if (!gradle.isFoss) {
        proprietaryCompile 'com.google.android.gms:play-services-location:10.0.1'
    }

    compile 'net.sourceforge.streamsupport:streamsupport:1.5.2'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'
}
