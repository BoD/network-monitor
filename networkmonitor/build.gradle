apply plugin: 'com.android.application'

android {
    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
    compileSdkVersion 22
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "ca.rmen.android.networkmonitor"
        minSdkVersion 9
        targetSdkVersion 22
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions {
        preDexLibraries = false
    }
}

project.tasks.withType(com.android.build.gradle.tasks.Dex) {
    additionalParameters=['--core-library']
}

dependencies {
    compile project (':awt-datatransfer-minimal')
    compile 'org.jraf:android-switch-backport:2.0.1'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.google.android.gms:play-services-location:7.0.0'
    compile 'com.afollestad:material-dialogs:0.7.2.4'
    compile ('net.sourceforge.jexcelapi:jxl:2.6.12') {
        exclude group: "log4j"
    }
    compile 'commons-net:commons-net:3.3'
    compile 'org.apache.commons:commons-email:1.3.3'
}
